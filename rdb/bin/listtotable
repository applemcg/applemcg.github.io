# listtotable - convert from list to table format
: ${TMP:=/tmp}
function awkorder {  awk '
NF     < 1     { next }
       { if (field[$1]) { next }
         field[$1] = ++fcount
         fnumb[fcount] = $1
       }
END    { for (i = 1; i <= fcount; i++)
               printf "%s ", fnumb[i]
       }'
}
trap 'rm -f $TMP/.ltt.$$' 0 1 2 3 15
cat > $TMP/.ltt.$$
awkorder="$(cat $TMP/.ltt.$$ | awkorder)"
fields $awkorder
( echo; cat $TMP/.ltt.$$;  ) | uniq | awk "
BEGIN  { nf = split(\"$awkorder\",rposn) }"'
BEGIN  { for (i = 1; i<=nf; i++) {
               posn[rposn[i]] = i
               }
       }
function send(f,i)     { printf f, field[i]; field[i]="" }
function record() { if (recorded) return
                       send("%s",i=1)  # send all fields,
       while(++i<=nf)  send("\t%s",i)  # some may have blanks
                       send("\n","")   # and follow with newline.
       recorded = 1;
       }
BEGIN  { FS = OFS = "\t" }
NF > 1 { field[posn[$1]] = $2; recorded = 0;next }
       { record() }
END    { record() }
'
