# gregorian:
#  from Practical Astronomy with Your Calculator, Peter Duffett-Smith
#  (C) Cambridge University Press, 1979,1981
# %doit% julian <gregt |tee julet|gregorian|diff - gregt| pg # >gregt.dif
#
case $1 in             #(
-c)    fmt=cf;shift;;  #(
-e)    fmt=sf;us=0;shift;;     #(
-u)    fmt=sf;us=1;shift;;
esac
: ${fmt:=cf}
: ${us:=0}
awk '
BEGIN  { d="%02d"; ds=d "/"; OFS="\t";
         cf=d d d; sf=ds ds d; fmt='"$fmt"'; us='"$us"'
}
function whdatef(fmt,yr,mo,da) {
       if (us)
               return sprintf(fmt,mo,da,yr)
       else
               return sprintf(fmt,yr,mo,da)
}
function dayofyr(yymmdd, yr) { return (yymmdd - int(365.25*yr)) }
function gregorian(jday, yymmdd,yr,mo,da  ) {
   yymmdd = jday - 1720997
   yr = int(yymmdd/365.25)
   da = dayofyr(yymmdd, yr)
   if(da < 123) { yr--; da = dayofyr(yymmdd, yr); }
   mo = int(da/30.6001) - 1
   da = da - int((mo+1)*30.6001)
   if (mo > 12) { yr += 1; mo -=12; }
   return whdatef(fmt,yr,mo,da)

}
   { $1 = gregorian($1); print }' $*
# eof
